plugins {
    id("uk.gov.justice.hmpps.gradle-spring-boot") version "3.1.7"
}

group 'uk.gov.justice.hmpps.prison'

configurations {
    compile.exclude module: 'commons-logging'
    compile.exclude module: 'log4j'
    compile.exclude module: 'c3p0'
    compile.exclude module: 'tomcat-jdbc'
}

// Exclude Serenity BDD integration and IntTest tests from 'test' task so they can be controlled independently
test {
    exclude '**/executablespecification/*', '**/*IntTest*'
    useJUnitPlatform()
}

dependencies {

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    implementation 'org.springframework:spring-jms:5.3.6'

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.1001')
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.0'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.0'
    implementation 'javax.activation:activation:1.1.1'

    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.github.jsqlparser:jsqlparser:4.0'
    implementation 'net.sf.ehcache:ehcache:2.10.6'
    implementation 'com.zaxxer:HikariCP:4.0.3'

    implementation 'io.springfox:springfox-boot-starter:3.0.0'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.oracle.database.jdbc:ojdbc10:19.10.0.0'

    compileOnly 'org.projectlombok:lombok:1.18.20'

    runtimeOnly 'org.hsqldb:hsqldb:2.5.1'
    runtimeOnly 'org.flywaydb:flyway-core:7.8.1'

    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation 'io.rest-assured:rest-assured:4.3.3'
    testImplementation 'io.rest-assured:json-schema-validator:4.3.3'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.3.3'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'

    testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.25.0'

    testImplementation 'net.serenity-bdd:serenity-core:2.1.8'
    testImplementation 'net.serenity-bdd:serenity-junit:2.1.8'
    testImplementation 'net.serenity-bdd:serenity-spring:2.1.8'
    testImplementation 'net.serenity-bdd:serenity-cucumber:1.9.51'
    testImplementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'
    testImplementation 'io.jsonwebtoken:jjwt:0.9.1'
    testImplementation 'org.glassfish:javax.el:3.0.0'
    testImplementation 'io.swagger.parser.v3:swagger-parser:2.0.20' // Keep this at 2.0.20 as advised here: https://github.com/ministryofjustice/prison-api/pull/748#discussion_r512664711

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
}

task testIntegration(type: Test) {
    include '**/*IntTest*'
    useJUnitPlatform()
    minHeapSize = "128m"
    maxHeapSize = "2048m"
    jvmArgs = ["--add-opens", "java.base/java.lang=ALL-UNNAMED", "--add-opens", "java.base/java.util=ALL-UNNAMED"]
}

task testWithSchemaNomis(type: Test) {
    def nomisEnvVars = ['api.db.target'   : 'nomis',
                        'cucumber.options': "--tags 'not (@wip or @broken)'"]

    exclude '**/persistence/*'
    exclude '**/*IntTest*'
    environment nomisEnvVars
    useJUnitPlatform()
    minHeapSize = "128m"
    maxHeapSize = "2048m"
    jvmArgs = ["--illegal-access=permit"]
}

task testWithSchemaNomisOracle(type: Test) {
    def nomisEnvVars = ['api.db.target'   : 'nomis', 'api.db.dialect': 'oracle',
                        'cucumber.options': "--tags 'not (@wip or @broken)'"]

    exclude '**/persistence/*'
    exclude '**/*IntTest*'
    environment nomisEnvVars
    useJUnitPlatform()
    jvmArgs = ["--illegal-access=permit"]
}

test.dependsOn testIntegration, testWithSchemaNomis

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "15"
    }
}
